PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Check whether at least one dataset has been changed since the last run
SELECT (?dateModified AS ?identifier) ?isChanged
WHERE {
  VALUES ?dataset {
    <https://data.colonialcollections.nl/nmvw/collection-archives>
    <https://data.colonialcollections.nl/Bronbeek/Stamboeken>
    <https://linkeddata.cultureelerfgoed.nl/rce/colonialobjects>
  }
  ?dataset a dcat:Dataset ;
    dct:modified ?rawDateModified .

  # Hack: the Dataset Register doesn't standardize on the datatypes of dates.
  # First convert the raw date to an integer before comparing it with the date on file
  BIND(xsd:integer(REPLACE(SUBSTR(STR(?rawDateModified), 1, 10), "-", "")) AS ?dateModified)

  # If ?_currentIdentifier is an empty string, this check has not been done before.
  # Make sure it results in '0' so as to flip ?isChanged to '1' and trigger a new run
  BIND(xsd:integer(IF("?_currentIdentifier" = "", 0, "?_currentIdentifier")) AS ?dateModifiedOfLastRun)

  # GraphDB gives a xsd:boolean but the integration layer (with Virtuoso) expects a number
  BIND(IF(?dateModified > ?dateModifiedOfLastRun, 1, 0) AS ?isChanged)
}
ORDER BY DESC(?isChanged) DESC(?dateModified)
LIMIT 1
