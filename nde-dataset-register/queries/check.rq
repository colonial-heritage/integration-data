PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Check whether at least one dataset has been changed since the last run
SELECT (?dateModified AS ?identifier) ?isChanged
WHERE {
  VALUES ?dataset {
    <https://data.colonialcollections.nl/nmvw/collection-archives>
    <https://data.colonialcollections.nl/Bronbeek/Stamboeken>
    <https://linkeddata.cultureelerfgoed.nl/rce/colonialobjects>
  }
  ?dataset a dcat:Dataset ;
    dct:modified ?rawDateModified .

  # Hack: the Dataset Register doesn't standardize on the datatypes of dates.
  # First convert the raw date to a string and then to XSD, before
  # comparing it with the date on file
  BIND(SUBSTR(STR(?rawDateModified), 1, 10) AS ?dateModifiedAsStr)
  BIND(xsd:date(?dateModifiedAsStr) AS ?dateModified)
  BIND("?_currentIdentifier"^^xsd:date AS ?dateModifiedOfLastRun)

  # GraphDB gives a xsd:boolean but the integration layer (with Virtuoso) expects a number
  BIND(IF(?dateModified > ?dateModifiedOfLastRun, 1, 0) AS ?isChanged)
}
ORDER BY DESC(?isChanged) DESC(?dateModified)
LIMIT 1
