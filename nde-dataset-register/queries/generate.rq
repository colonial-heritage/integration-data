###############################
# Query for generating a graph for the Knowledge Graph with dataset descriptions from the NDE Dataset Register.
# It uses Schema.org, the unified model of the Knowledge Graph for organizations and datasets.
# Derived from https://triplestore.netwerkdigitaalerfgoed.nl/sparql?savedQueryName=Full%20dataset%20descriptions%20for%20publisher&owner=admin&execute
###############################

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX schema: <https://schema.org/>

CONSTRUCT {
  ?this a schema:Dataset ;
    schema:name ?title ;
    schema:license ?license ;
    schema:publisher ?publisher ;
    schema:description ?description ;
    schema:keywords ?keyword ;
    schema:mainEntityOfPage ?landingPage ;
    schema:dateCreated ?created ;
    schema:dateModified ?modified ;
    schema:datePublished ?issued ;
    schema:inLanguage ?language ;
    schema:distribution ?distribution .

  ?distribution schema:contentUrl ?distributionUrl ;
    schema:encodingFormat ?distributionFormat .

  ?publisher a schema:Organization ;
    schema:name ?publisherName .
}
WHERE {
  VALUES ?this {
    ?_iris
  }

  ?this a dcat:Dataset ;
    dct:title ?title ;
    dct:license ?license ; # Licenses can be literals, not IRIs. Convert if necessary: BIND(IRI(?tmpLicense) AS ?license)
    dct:publisher ?publisher .

  OPTIONAL { ?this dct:description ?description }
  OPTIONAL { ?this dcat:keyword ?keyword }
  OPTIONAL { ?this dcat:landingPage ?landingPage }
  OPTIONAL { ?this dct:created ?created } # Datatypes can be e.g. XSD, Schema.org, or missing...
  OPTIONAL { ?this dct:modified ?modified } # Datatypes can be e.g. XSD, Schema.org, or missing...
  OPTIONAL { ?this dct:issued ?issued } # Datatypes can be e.g. XSD, Schema.org, or missing...
  OPTIONAL { ?this dct:language ?language }

  OPTIONAL {
    ?this dcat:distribution ?distribution .
    ?distribution dcat:accessURL ?distributionUrl ;
      dcat:mediaType ?distributionFormat .
  }

  ?publisher a foaf:Organization ;
    foaf:name ?publisherName .
}
