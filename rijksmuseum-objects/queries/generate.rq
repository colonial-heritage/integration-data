###############################
# Query for generating a search graph from dataset 'Rijksmuseum Objects'
###############################

PREFIX cc: <https://colonialcollections.nl/schema#> # Internal ontology
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?this a cc:HeritageObject ;
    #cc:additionalType_en ?typeNameEn ;
    #cc:additionalType_nl ?typeNameNl ;
    cc:identifier ?identifier ;
    cc:name ?name ;
    cc:description ?description ; # Only for indexing
    #cc:material_en ?materialNameEn ;
    #cc:material_nl ?materialNameNl ;
    cc:inscription ?inscription ; # Only for indexing
    #cc:creator ?creatorName ;
    cc:yearCreatedStart ?beginOfTheBeginYear ;
    cc:yearCreatedEnd ?endOfTheEndYear ;
    #cc:locationCreated ?locationCreatedName ; # Only for indexing
    #cc:countryCreated_en ?countryCreatedNameEn ;
    #cc:countryCreated_nl ?countryCreatedNameNl ;
    #cc:alternateName ?alternateLocationCreatedName, ?alternateCountryCreatedName ; # Only for indexing
    cc:publisher_en ?publisherNameEn ;
    cc:publisher_nl ?publisherNameNl .
}
WHERE {
  VALUES ?this {
    ?_iris
  }

  ####################
  # Type. Currently ignored because it does not use terms from the Wereldmuseum thesaurus
  ####################

  #OPTIONAL {
  #  ?this crm:P2_has_type [
  #    skos:prefLabel ?typeName
  #  ] .

  #  BIND(IF(LANG(?typeName) = "en", ?typeName, ?null) AS ?typeNameEn)
  #  BIND(IF(LANG(?typeName) = "nl", ?typeName, ?null) AS ?typeNameNl)
  #}

  ####################
  # Identifier
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E42_Identifier ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404626> ; # Identification number
      crm:P190_has_symbolic_content ?identifier
    ] .
  }

  ####################
  # Name - both in Dutch and English
  ####################

  OPTIONAL {
    ?this crm:P1_is_identified_by [
      a crm:E33_E41_Linguistic_Appellation ;
      crm:P2_has_type <http://vocab.getty.edu/aat/300404670> ; # Name
      crm:P190_has_symbolic_content ?name
    ] .
  }

  ####################
  # Description - both in Dutch and English
  ####################

  OPTIONAL {
    ?this crm:P67i_is_referred_to_by ?descriptionNode .
    ?descriptionNode crm:P2_has_type <http://vocab.getty.edu/aat/300435416> ; # Description
      crm:P190_has_symbolic_content ?fullDescription .

    # Elasticsearch does not index strings longer than a certain length
    BIND(SUBSTR(?fullDescription, 0, 256) AS ?description)
  }

  ####################
  # Subject
  ####################
  # TBD: not in the KG yet (crm:P62_depicts)

  ####################
  # Material. Currently ignored because it does not use terms from the Wereldmuseum thesaurus
  ####################

  #OPTIONAL {
  #  ?this crm:P45_consists_of [
  #    skos:prefLabel ?materialName
  #  ] .

  #  BIND(IF(LANG(?materialName) = "en", ?materialName, ?null) AS ?materialNameEn)
  #  BIND(IF(LANG(?materialName) = "nl", ?materialName, ?null) AS ?materialNameNl)
  #}

  ####################
  # Inscription - both in Dutch and English
  # Example: https://n2t.net/ark:/27023/01cddb5b67c8fcbebac86664e57f3efc
  ####################

  OPTIONAL {
    ?this crm:P128_carries [
      crm:P2_has_type <http://vocab.getty.edu/aat/300435414> ; # Inscription description
      crm:P190_has_symbolic_content ?inscription
    ] .
  }

  ####################
  # Technique. Currently ignored because it does not use terms from the Wereldmuseum thesaurus
  ####################

  #OPTIONAL {
  #  ?this crm:P108i_was_produced_by/crm:P32_used_general_technique [
  #    skos:prefLabel ?techniqueName
  #  ]

  #  BIND(IF(LANG(?techniqueName) = "en", ?techniqueName, ?null) AS ?techniqueNameEn)
  #  BIND(IF(LANG(?techniqueName) = "nl", ?techniqueName, ?null) AS ?techniqueNameNl)
  #}

  ####################
  # Creator. Currently ignored because the Rijksmuseum creator data isn't in the KG yet
  ####################

  #OPTIONAL {
  #  ?this crm:P108i_was_produced_by/crm:P14_carried_out_by [
  #    crm:P190_has_symbolic_content ?creatorName ;
  #  ] .
  #}

  ####################
  # Date of creation
  ####################

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?beginOfTheBegin .
    FILTER(DATATYPE(?beginOfTheBegin) IN (xsd:date, xsd:gYear, xsd:gYearMonth, xsd:dateTime))
  }

  BIND(IF(BOUND(?beginOfTheBegin), YEAR(?beginOfTheBegin), ?null) AS ?beginOfTheBeginYear)

  OPTIONAL {
    ?this crm:P108i_was_produced_by/crm:P4_has_time-span/crm:P82b_end_of_the_end ?endOfTheEnd .
    FILTER(DATATYPE(?endOfTheEnd) IN (xsd:date, xsd:gYear, xsd:gYearMonth, xsd:dateTime))
  }

  BIND(IF(BOUND(?endOfTheEnd), YEAR(?endOfTheEnd), ?null) AS ?endOfTheEndYear)

  ####################
  # Location of creation. Currently ignored because it does not use terms from GeoNames (although it has la:equivalent with TGN)
  ####################

  #OPTIONAL {
  #  ?this crm:P108i_was_produced_by/crm:P7_took_place_at ?location .
  #}

  ####################
  # Part of dataset
  ####################

  ?this la:member_of ?dataset .

  ####################
  # Publisher of dataset
  ####################

  OPTIONAL {
    ?dataset schema:publisher/schema:name ?publisherName

    BIND(IF(LANG(?publisherName) = "en", ?publisherName, ?null) AS ?publisherNameEn)
    BIND(IF(LANG(?publisherName) = "nl", ?publisherName, ?null) AS ?publisherNameNl)
  }
}
